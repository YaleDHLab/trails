<!DOCTYPE html>
<html>
  <head>
    <meta charset='UTF-8'>
    <link href='assets/style.css' rel='stylesheet' type='text/css'/>
  </head>
  <body class=''>
    <!-- rendered vert shader -->
    <script type='x-shader/x-vertex' id='vertex-shader'>
    precision highp float;

    uniform mat4 modelViewMatrix;
    uniform mat4 projectionMatrix;
    uniform vec3 cameraPosition;
    uniform float dpi;
    uniform float height;
    uniform sampler2D touchtexture;

    attribute vec3 position;
    attribute vec3 translation;
    attribute vec3 color;
    attribute vec3 clickColor;

    varying vec3 vColor;
    varying vec3 vClickColor;
    varying float vSize;

    void main() {
      // position and project the point
      vec4 mvPosition = modelViewMatrix * vec4(position + translation, 1.0);
      gl_Position = projectionMatrix * mvPosition;

      // convert gl_Position to 0:1 on each axis for touchtexture lookups
      float x = (gl_Position.x + cameraPosition.z) / (2.0 * cameraPosition.z);
      float y = (gl_Position.y + cameraPosition.z) / (2.0 * cameraPosition.z);

      // scale the point size by the device DPI and window height
      gl_PointSize = 0.0004 * dpi * height / -mvPosition.z;

      // adjust the point size based on the touchtexture
      vec4 brightness = texture2D(touchtexture, vec2(x, y));
      float touchSize = gl_PointSize * 200.0 * brightness.r * cameraPosition.z;
      gl_PointSize = max(gl_PointSize, touchSize);
      gl_PointSize = max(gl_PointSize, 1.0);

      // pass varyings
      vColor = color;
      vClickColor = clickColor;
      vSize = gl_PointSize;
    }
    </script>

    <!-- rendered frag shader -->
    <script type='x-shader/x-fragment' id='fragment-shader'>
    precision highp float;

    uniform sampler2D grit;
    uniform float useColor;
    uniform float useNightMode;

    varying vec3 vColor;
    varying vec3 vClickColor;
    varying float vSize;

    void main() {
      // get point shape
      if (length(gl_PointCoord - vec2(0.5)) > 0.5) discard;

      if (useNightMode > 0.5) {
        gl_FragColor = vec4(vColor, 1.0);
      } else {
        gl_FragColor = texture2D(grit, gl_PointCoord * vSize / 200.0); // grit
      }

      // overwrite colors if we're GPU picking
      if (useColor > 0.5) gl_FragColor = vec4(vClickColor, 1.0);
    }
    </script>

    <label class='switch'>
      <input class='switch__input' type='checkbox'>
      <span class='switch__background'>
        <span class='switch__toggle'>
          <span class='switch__moon'></span>
        </span>
        <span class='switch__stars'></span>
        <span class='switch__clouds'></span>
      </span>
    </label>

    <div id='canvas-container'>
      <div id='tooltip'></div>
    </div>
    <script src='assets/js/three.min.js'></script>
    <script src='assets/js/trackball-controls.js'></script>
    <script src='assets/js/gunzip.min.js'></script>
    <script src='assets/js/stats.js'></script>
    <script src='assets/js/luminosity.js'></script>
    <script src='assets/js/effect-composer.js'></script>
    <script src='assets/js/unreal-bloom.js'></script>
    <script src='assets/js/script.js'></script>
  </body>
</html>